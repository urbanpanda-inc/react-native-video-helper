def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    if (project == rootProject) {
        repositories {
            jcenter()
        }

        dependencies {
            //noinspection GradleDependency
            classpath 'com.android.tools.build:gradle:2.3.2'
        }
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 28)
    buildToolsVersion safeExtGet('buildToolsVersion', '28.0.3')

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 28)
    }

    lintOptions {
        abortOnError false
        warning 'InvalidPackage'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.googlecode.mp4parser:isoparser:1.0.6'
    implementation 'org.aspectj:aspectjrt:1.7.3'
    implementation 'com.facebook.react:react-native:+'
}
  